@echo off
:: Pocket TTS server launcher
cd /d "%~dp0.."

:: Find conda base directory
set "CONDA_BASE="
for %%D in (
    "%USERPROFILE%\miniconda3"
    "%USERPROFILE%\anaconda3"
    "C:\ProgramData\miniconda3"
    "C:\ProgramData\anaconda3"
) do (
    if exist "%%~D\Scripts\conda.exe" (
        set "CONDA_BASE=%%~D"
        goto :found_conda
    )
)

:found_conda
if not defined CONDA_BASE (
    echo [ERROR] Conda not found. Please install Miniconda or ensure it's in the standard location.
    pause
    exit /b 1
)

call "%CONDA_BASE%\Scripts\activate.bat" "pocket_tts"
if errorlevel 1 (
    echo [ERROR] Failed to activate Pocket TTS environment
    pause
    exit /b 1
)

echo [INFO] Starting Pocket TTS server...
echo.

:: Run from the servers folder
cd /d "%~dp0..\servers"
python -X faulthandler -u pocket_tts_server.py --port 8894
if errorlevel 1 (
    echo [ERROR] Pocket TTS server crashed!
    pause
)
pause
